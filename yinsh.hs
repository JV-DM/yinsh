import Haste
import Haste.Graphics.Canvas
import Data.List (minimumBy)
import Data.IORef

-- >>> import Test.QuickCheck

-- Color theme
-- http://www.colourlovers.com/palette/15/tech_light
green = RGB 209 231  81
blue  = RGB  38 173 228
white = RGB 255 255 255

-- Dimensions
spacing         = 60 :: Double
markerWidth     = 20 :: Double
ringInnerRadius = 22 :: Double
ringWidth       = 6 :: Double
originX         = -15 :: Double
originY         = 140 :: Double

-- | Yinsh hex coordinates
type YCoord = (Int, Int)

-- | Player types: black & white or blue & green
data Player = B | W

instance Enum Player where
    succ B = W
    succ W = B

-- | Translate hex coordinates to screen coordinates
fromCoord :: YCoord -> Point
fromCoord (ya, yb) = (0.5 * sqrt 3 * x' + originX,
                      y' - 0.5 * x' + originY)
                where x' = spacing * fromIntegral ya
                      y' = spacing * fromIntegral yb

-- could be generated by generating all triangular lattice points smaller
-- than a certain cutoff (~ 5)
numPoints :: [[Int]]
numPoints = [[2..5], [1..7], [1..8], [1..9],
             [1..10], [2..10], [2..11], [3..11],
             [4..11], [5..11], [7..10]]

-- | All points on the pBoard
-- >>> length coords
-- 85
coords :: [YCoord]
coords = concat $ zipWith (\list ya -> map (\x -> (ya, x)) list) numPoints [1..]

-- | Check if two points are connected by a line
-- >>> connected (3, 4) (8, 4)
-- True
--
-- prop> ((\c1 c2 -> connected c1 c2 == connected c2 c1) :: YCoord -> YCoord -> Bool)
--
connected :: YCoord -> YCoord -> Bool
connected (x, y) (a, b) =        x == a
                          ||     y == b
                          || x - y == a - b

-- | List of points reachable from a certain point
reachable :: YCoord -> [YCoord]
reachable c = filter (connected c) coords

data DisplayState = BoardOnly GameState
                  | WaitTurn GameState

data Element = Ring Player YCoord
             | Marker Player YCoord

data TurnMode = AddRing
              | AddMarker
              | MoveRing YCoord
              | RemoveRing

type Board = [Element]

data GameState = GameState {
                             activePlayer :: Player,
                             turnMode :: TurnMode,
                             board :: Board
                           }

-- | All grid points as screen coordinates
points :: [Point]
points = map fromCoord coords

-- | Translate by hex coordinate
translateC :: YCoord -> Picture () -> Picture ()
translateC = translate . fromCoord

playerColor :: Player -> Color
playerColor B = blue
playerColor W = green

setPlayerColor :: Player -> Picture ()
setPlayerColor = setFillColor . playerColor

pRing :: Player -> Picture ()
pRing p = do
    setPlayerColor p
    fill circL
    stroke circL
    setFillColor white
    fill circS
    stroke circS
    pCross ringInnerRadius
        where circL = circle (0, 0) (ringInnerRadius + ringWidth)
              circS = circle (0, 0) ringInnerRadius

pMarker :: Player -> Picture ()
pMarker p = do
    setPlayerColor p
    fill circ
    stroke circ
        where circ = circle (0, 0) markerWidth

pElement :: Element -> Picture ()
pElement (Ring p c)   = translateC c $ pRing p
pElement (Marker p c) = translateC c $ pMarker p

pCross :: Double -> Picture ()
pCross len = do
    l
    rotate (2 * pi / 3) l
    rotate (4 * pi / 3) l
        where l = stroke $ line (0, -len) (0, len)

pDot :: Picture ()
pDot = do
    setFillColor $ RGB 0 0 0
    fill $ circle (0, 0) 5

pBoard :: Board -> Picture ()
pBoard board = do
    sequence_ $ mapM translate points (pCross (0.5 * spacing))
    -- sequence_ $ mapM (translate . fromCoord) (reachable (3, 6)) pDot
    mapM_ pElement board

pAction :: TurnMode -> YCoord -> Player -> Picture ()
pAction AddMarker mc p = pElement (Marker p mc)
pAction AddRing mc p = pElement (Ring p mc)

-- | Render everything that is seen on the screen
pDisplay :: DisplayState
         -> YCoord         -- ^ Coordinate close to mouse cursor
         -> Picture ()
pDisplay (BoardOnly gs) _ = pBoard (board gs)
pDisplay (WaitTurn gs) mc = pBoard (board gs) >> pAction (turnMode gs) mc (activePlayer gs)
-- pDisplay ConnectedPoints c = do
--     pBoard
--     sequence_ $ mapM (translate . fromCoord) (reachable c) pDot

getClosestCoord :: Point -> YCoord
getClosestCoord (x, y) = coords !! snd lsort
    where lind = zipWith (\p i -> (dist p, i)) points [0..]
          lsort = minimumBy cmpFst lind
          dist (x', y') = (x-x')^2 + (y-y')^2
          cmpFst t1 t2 = compare (fst t1) (fst t2)

testBoard :: Board
testBoard = [
    Ring B (3, 4),
    Ring B (4, 9),
    Ring W (8, 7),
    Ring W (6, 3),
    Ring W (4, 8),
    Marker W (6, 4),
    Marker W (6, 5),
    Marker W (6, 7),
    Marker B (6, 6)]

testGameState = GameState {
    activePlayer = B,
    turnMode = AddMarker,
    board = testBoard
}

testDisplayState = WaitTurn testGameState

showMoves :: Canvas -> DisplayState -> (Int, Int) -> IO ()
showMoves can ds point = render can
                          $ pDisplay ds (coordFromXY point)

coordFromXY :: (Int, Int) -> YCoord
coordFromXY (x, y) = getClosestCoord (fromIntegral x, fromIntegral y)

main :: IO ()
main = do
    Just can <- getCanvasById "canvas"
    Just ce <- elemById "canvas"

    ioDS <- newIORef testDisplayState

    -- draw initial board
    render can (pBoard testBoard)

    ce `onEvent` OnMouseMove $ \point -> do
        ds <- readIORef ioDS
        showMoves can ds point

    ce `onEvent` OnClick $ \button point -> do
        modifyIORef' ioDS $ \(WaitTurn gs) ->
            WaitTurn (
                gs {
                    board = Marker (activePlayer gs) (coordFromXY point) : board gs,
                    activePlayer = succ (activePlayer gs)
                })
        return ()
    return ()
